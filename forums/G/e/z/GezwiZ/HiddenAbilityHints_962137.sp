/* Plugin Template generated by Pawn Studio */

#include <sourcemod>

#define CVAR_FLAGS FCVAR_PLUGIN
#define PLUGIN_VERSION "1.3"

#define TEXT_SMOKER_RELEASE "Press the MELEE key to let go of your victim."
#define TEXT_SURVIVOR_CRAWL "Press the FORWARD key to crawl."
#define TEXT_HUNTER_RELEASE "Press the POUNCE key to jump off of your victim."
#define L4D_MAXCLIENTS 19

#define SURVIVORTEAM 2

new playerInfo[L4D_MAXCLIENTS];

public Plugin:myinfo = 
{
	name = "[L4D] Hidden Ability Hints",
	author = "Thraka",
	description = "Informs a player of hidden abilities they can use, at the appropriate time.",
	version = PLUGIN_VERSION,
	url = "http://forums.alliedmods.net/showthread.php?p=862626"
}

new Handle:g_hConVar_Crawling;
new Handle:g_hConVar_SmokerRelease;
new Handle:g_hConVar_HunterRelease;

new bool:isDownedEventHooked = false;
new bool:isPulledEventHooked = false;
new bool:isPouncedEventHooked = false;

public OnPluginStart()
{
	CreateConVar("l4d_ability_hint_ver", PLUGIN_VERSION, "Version of the hidden abilities hint plugin.", FCVAR_PLUGIN|FCVAR_SPONLY|FCVAR_NOTIFY);
	
	g_hConVar_Crawling = FindConVar("survivor_allow_crawling");
	g_hConVar_SmokerRelease = FindConVar("tongue_allow_voluntary_release");
	g_hConVar_HunterRelease = FindConVar("z_lunge_release");

	// Survivor crawl
	SetCrawl();
	
	// Smoker release
	SetSmokerRelease();
	
	// Hunter release
	SetHunterRelease();
	
	// Get the console var change events for the ones we care about
	HookConVarChange(g_hConVar_Crawling, ConVarChange_Crawl);
	HookConVarChange(g_hConVar_SmokerRelease, ConVarChange_SmokerRelease);
	HookConVarChange(g_hConVar_HunterRelease, ConVarChange_HunterRelease);
}

public ConVarChange_Crawl(Handle:convar, const String:oldValue[], const String:newValue[])
{
	SetCrawl();
}

public ConVarChange_SmokerRelease(Handle:convar, const String:oldValue[], const String:newValue[])
{
	SetSmokerRelease();
}

public ConVarChange_HunterRelease(Handle:convar, const String:oldValue[], const String:newValue[])
{
	SetHunterRelease();
}

public Event_SmokerGrabbed(Handle:event, const String:name[], bool:dontBroadcast)
{
	new attackerClient = GetClientOfUserId(GetEventInt(event, "userid"));
	PrintHintText(attackerClient, TEXT_SMOKER_RELEASE);
}

public Event_HunterPounced(Handle:event, const String:name[], bool:dontBroadcast)
{
	new attackerClient = GetClientOfUserId(GetEventInt(event, "userid"));
	PrintHintText(attackerClient, TEXT_HUNTER_RELEASE);
}

public Event_SurvivorDown(Handle:event, const String:name[], bool:dontBroadcast)
{
	new survivorClient = GetClientOfUserId(GetEventInt(event, "userid"));
	
	if (playerInfo[survivorClient] != 1)
		if (GetClientTeam(survivorClient) == SURVIVORTEAM)
			PrintHintText(survivorClient, TEXT_SURVIVOR_CRAWL);
}

public Event_SurvivorGrabbed(Handle:event, const String:name[], bool:dontBroadcast)
{
	
	new survivorClient = GetClientOfUserId(GetEventInt(event, "victim"));
	playerInfo[survivorClient] = 1;
}

public Event_SurvivorLetGo(Handle:event, const String:name[], bool:dontBroadcast)
{
	
	if (event != INVALID_HANDLE)
	{	
		new survivorClient = GetClientOfUserId(GetEventInt(event, "victim"));
		
		if (survivorClient != 0)
		{
			playerInfo[survivorClient] = 0;
			
			// Check to see if they are incap, if so, print to them
			new offset = FindSendPropInfo("CTerrorPlayer", "m_isIncapacitated");
			if (offset > 0)
			{
				if (GetEntData(survivorClient, offset, 1))
					PrintHintText(survivorClient, TEXT_SURVIVOR_CRAWL);
			}
		}
	}
	
}

// Check the console variable for if crawling is enabled and configure
//
SetCrawl()
{
	if (GetConVarInt(g_hConVar_Crawling) == 1)
	{
		if (isDownedEventHooked == false)
		{
			HookEvent("player_incapacitated", Event_SurvivorDown);
			
			HookEvent("lunge_pounce", Event_SurvivorGrabbed);
			HookEvent("pounce_end", Event_SurvivorLetGo);
			
			HookEvent("tongue_grab", Event_SurvivorGrabbed);
			HookEvent("tongue_release", Event_SurvivorLetGo);
			
			isDownedEventHooked = true;
		}
	}
	else
	{
		if (isDownedEventHooked == true)
		{
			UnhookEvent("player_incapacitated", Event_SurvivorDown);
			isDownedEventHooked = false;
		}
	}		
}

SetSmokerRelease()
{
	if (GetConVarInt(g_hConVar_SmokerRelease) == 1)
	{
		if (isPulledEventHooked == false)
		{
			HookEvent("tongue_grab", Event_SmokerGrabbed);
			isPulledEventHooked = true;
		}
	}
	else
	{
		if (isPulledEventHooked == true)
		{
			UnhookEvent("tongue_grab", Event_SmokerGrabbed);
			isPulledEventHooked = false;
		}
	}	
}

SetHunterRelease()
{
	if (GetConVarInt(g_hConVar_HunterRelease) == 1)
	{
		if (isPouncedEventHooked == false)
		{
			HookEvent("lunge_pounce", Event_HunterPounced);
			isPouncedEventHooked = true;
		}
	}
	else
	{
		if (isPouncedEventHooked == true)
		{
			UnhookEvent("lunge_pounce", Event_HunterPounced);
			isPouncedEventHooked = false;
		}
	}	
}
