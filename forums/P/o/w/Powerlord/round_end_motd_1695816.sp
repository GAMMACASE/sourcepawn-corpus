/* Plugin Template generated by Pawn Studio */

#pragma semicolon 1
#include <sourcemod>

#define VERSION "1.1"

new g_Round = 0;

// Our Cvars
new Handle:g_Cvar_Rounds = INVALID_HANDLE;

// Valve Cvars
new Handle:g_Cvar_obBonusTime = INVALID_HANDLE;
new Handle:g_Cvar_dodBonusTime = INVALID_HANDLE;

public Plugin:myinfo = 
{
	name = "Round End MOTD",
	author = "Powerlord",
	description = "Show the MOTD at the end of the round.",
	version = VERSION,
	url = "https://forums.alliedmods.net/showthread.php?t=163598"
}

public OnPluginStart()
{
	CreateConVar("remotd_version", VERSION, "Round End MOTD Version", FCVAR_DONTRECORD | FCVAR_NOTIFY);
	g_Cvar_Rounds = CreateConVar("remotd_rounds", "3", "Rounds between showing MOTD", FCVAR_NONE, true, 1.0);

	// Most games
	HookEventEx("round_end", Event_RoundEnd);
	// TF2
	HookEventEx("teamplay_round_win", Event_RoundEnd);
	
	g_Cvar_obBonusTime = FindConVar("mp_bonusroundtime");
	g_Cvar_dodBonusTime = FindConVar("dod_bonusroundtime");
}

public Event_RoundEnd(Handle:event, const String:name[], bool:dontBroadcast)
{
	if (++g_Round % GetConVarInt(g_Cvar_Rounds) == 0)
	{
		// Prevents overflow by resetting to 0 every 3 rounds
		g_Round = 0;
		
		new Float:bonusTime = 0.0;
		if (g_Cvar_obBonusTime != INVALID_HANDLE)
		{
			bonusTime = GetConVarFloat(g_Cvar_obBonusTime);
		}
		else if (g_Cvar_dodBonusTime != INVALID_HANDLE)
		{
			bonusTime = GetConVarFloat(g_Cvar_dodBonusTime);
		}
		
		CreateTimer(bonusTime, Timer_MOTD, INVALID_HANDLE, TIMER_FLAG_NO_MAPCHANGE);
	}
}

public Action:Timer_MOTD(Handle:timer)
{
	for (new i = 1; i <= MaxClients; i++)
	{
		if (IsClientInGame(i) && !IsFakeClient(i))
		{
			// Taken from basetriggers.sp
			ShowMOTDPanel(i, "Message Of The Day", "motd", MOTDPANEL_TYPE_INDEX);
		}
	}
	return Plugin_Continue;
}